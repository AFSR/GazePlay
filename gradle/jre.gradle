String.metaClass.encodeURL = {
    java.net.URLEncoder.encode(delegate, "UTF-8")
}

String.metaClass.decodeURL = {
    java.net.URLDecoder.decode(delegate, "UTF-8")
}

ext.jdkSource = 'https://github.com/AdoptOpenJDK/openjdk11-binaries/releases/download'
ext.jdkVersion = '11.0.6_10'
ext.jdkTag = jdkVersion.replace('_', '+').encodeURL()
ext.jdkFolder = "jdk-${jdkVersion.replace('_', '+')}"

ext.jmodsVersion = '11.0.2'
ext.jmodsSource = "https://gluonhq.com/download/javafx-${jmodsVersion.replace('.', '-')}-jmods"

ext.downloadLocation = new File(project.repositories.mavenLocal().url).getAbsolutePath();

apply from: "${rootDir}/gradle/jre/linux.gradle"
apply from: "${rootDir}/gradle/jre/macos.gradle"
apply from: "${rootDir}/gradle/jre/windows.gradle"

runtime {
    options = ['--compress', '2', '--no-header-files', '--no-man-pages']
    modules = jreModules
    jreDir = file("${buildDir}/jre")

    targetPlatform('linux-x64', "${project.ext.downloadLocation}/jre/linux-x64/${project.ext.jdkFolder}")
    targetPlatform('windows-x64', "${project.ext.downloadLocation}/jre/windows-x64/${project.ext.jdkFolder}")
    targetPlatform('windows-x86', "${project.ext.downloadLocation}/jre/windows-x86/${project.ext.jdkFolder}")
    targetPlatform('macos', "${project.ext.downloadLocation}/jre/macos/${project.ext.jdkFolder}/Contents/Home")
}

task clearJREFolder(type: Delete) {
    delete '*.tar.gz', '*.zip'
}

task('downloadAndExtractJDKs', group: 'custom jre') {
    dependsOn addJmodsToLinux64JDK, addJmodsToWindows64JDK, addJmodsToWindows86JDK, addJmodsToMacosJDK
    finalizedBy clearJREFolder
}
