plugins {
    id 'application'
    id 'distribution'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'io.freefair.lombok' version '4.0.1'
    id 'com.github.hierynomus.license-report' version '0.15.0'
    id 'com.github.spotbugs' version '2.0.0'
    id "com.github.johnrengelman.shadow" version "5.1.0"
}

apply from: "${rootDir}/gradle/package.gradle"

// Add -PSpotBugs=true to the Gradle Command Line to run spotbugs.
String value = project.getProperties().get('SpotBugs')
boolean SpotBugs = Boolean.parseBoolean(value) ?: false

allprojects {
    group = 'com.github.schwabdidier'
    version = '1.6.2-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()

        maven {
            url = 'https://jitpack.io'
        }

        maven {
            url = 'https://raw.github.com/agomezmoron/screen-recorder/mvn-repo'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.openjfx.javafxplugin'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'com.github.hierynomus.license-report'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'distribution'
    apply plugin: 'com.github.johnrengelman.shadow'

    javafx {
        version = "11.0.2"
        modules = [ 'javafx.controls', 'javafx.swing', 'javafx.media', 'javafx.web' ]
    }

    spotbugs {
        toolVersion = '4.0.0-beta3'
    }

    spotbugsMain.enabled = SpotBugs
    spotbugsTest.enabled = SpotBugs

    downloadLicenses {
        dependencyConfiguration = 'compileClasspath'
        includeProjectDependencies = true
    }

    dependencies {}

    sourceCompatibility = '11'

    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

dependencies {
    runtime project(':gazeplay')
    runtime project(':gazeplay-commons')
    runtime project(':gazeplay-data')
    runtime project(':gazeplay-melordi')
    runtime project(':gazeplay-picto-pick')
}

distributions {
    shadow {
        contents {
            from "gazeplay-dist/src/main/bin"
            from "build/reports"
        }
    }
}

mainClassName = 'net.gazeplay.GazePlayLauncher'

defaultTasks 'clean', 'build'
